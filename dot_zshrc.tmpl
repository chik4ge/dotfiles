# source command override
function source {
  ensure_zcompiled $1
  builtin source $@
}
function ensure_zcompiled {
  local compiled="$1.zwc"
  if [[ ! -r "$compiled" || "$1" -nt "$compiled" ]]; then
    echo "\033[1;36mCompiling\033[m $1"
    zcompile $1
  fi
}
ensure_zcompiled ~/.zshrc

# init homebrew to run sheldon
{{ if eq .chezmoi.os "darwin" -}}
eval "$(/opt/homebrew/bin/brew shellenv)"
{{ else if eq .chezmoi.os "linux" -}}
eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
{{ end -}}

# load sheldon with cache
cache_dir=${XDG_CACHE_HOME:-$HOME/.cache}
sheldon_cache="$cache_dir/sheldon.zsh"
sheldon_toml="$HOME/.config/sheldon/plugins.toml"
# if cache does not exist or outdated, create it
if [[ ! -r "$sheldon_cache" || "$sheldon_toml" -nt "$sheldon_cache" ]]; then
  mkdir -p $cache_dir
  sheldon source >$sheldon_cache
fi
source "$sheldon_cache"
unset cache_dir sheldon_cache sheldon_toml
# eval "$(sheldon source)"

# load settings that require snyc
source ~/.config/zsh/sync.zsh

# load settings that don't require snyc
zsh-defer source ~/.config/zsh/lazy.zsh

# unbind source override
zsh-defer unfunction source

# Start tmux only for interactive shells with TTY
if [[ $- == *i* ]] && [[ -t 0 ]] && command -v tmux >/dev/null 2>&1 && [[ ! -n $TMUX && $TERM_PROGRAM != "vscode" ]]; then
  SESSION_NAME="${TMUX_SESSION_NAME:-WSL}"
  
  if tmux has-session -t "$SESSION_NAME" 2>/dev/null; then
    exec tmux attach-session -t "$SESSION_NAME"
  else
    exec tmux new-session -s "$SESSION_NAME" -n main
  fi
fi
